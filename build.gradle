/*
 * This file is part of Guru Cue Search & Recommendation Engine.
 * Copyright (C) 2017 Guru Cue Ltd.
 *
 * Guru Cue Search & Recommendation Engine is free software: you can
 * redistribute it and/or modify it under the terms of the GNU General
 * Public License as published by the Free Software Foundation, either
 * version 3 of the License, or (at your option) any later version.
 *
 * Guru Cue Search & Recommendation Engine is distributed in the hope
 * that it will be useful, but WITHOUT ANY WARRANTY; without even the
 * implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 * See the GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Guru Cue Search & Recommendation Engine. If not, see
 * <http://www.gnu.org/licenses/>.
 */
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'application'
sourceCompatibility = 1.8
targetCompatibility = 1.8

// to tag manually and push the tag upstream: V=1.0.101; git tag -a rls_$V -m "AI build $V" && git push -q origin rls_$V

group = 'com.gurucue.recommendations'
version = hasProperty('build_version') ? build_version : GitVersion.getVersion().toString()
mainClassName = 'com.gurucue.recommendations.ModelBuilder'
applicationDefaultJvmArgs = ['-XX:MaxPermSize=128m', '-Xms16384m', '-Xmx24576m', '-XX:+UseConcMarkSweepGC', '-XX:+CMSIncrementalMode']

repositories {
    flatDir dirs: 'libs'
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile 'org.apache.logging.log4j:log4j-api:2.2'
    compile 'org.apache.logging.log4j:log4j-core:2.2'
    compile 'net.sf.trove4j:trove4j:3.0.3'
    compile 'com.gurucue.recommendations:database:1.0.0'
    compile 'com.gurucue.recommendations:data-provider-jdbc:1.0.0'
    compile 'com.gurucue.recommendations:data-provider-postgresql:1.0.0'
    compile 'tw.edu.ntu.csie:libsvm:3.16'
    compile 'de.congrace:exp4j:0.3.8'
    compile 'commons-daemon:commons-daemon:1.0.15'

    testCompile 'javassist:javassist:3.12.1.GA'
    testCompile 'junit:junit:4.11'
}

test {
    testLogging.showStandardStreams = true
    minHeapSize = "1024m"
    maxHeapSize = "3072m"
    environment("JDBC_URL", "jdbc:postgresql://localhost/demo")
    environment("JDBC_USERNAME", "demo")
    environment("JDBC_PASSWORD", "demo")
}

jar {
	manifest {
		attributes(
			'Implementation-Title': 'Recommendations AI engine',
			'Implementation-version': version,
			'Built-By': System.properties.'user.name'
		)
	}
	setBaseName 'ai'
}

task buildModel(type: JavaExec, dependsOn: sourceSets.main.runtimeClasspath) {
    main = 'com.gurucue.recommendations.ModelBuilder'
    classpath = sourceSets.main.runtimeClasspath
    args = []
    minHeapSize = "16384m"
    maxHeapSize = "30720m"
}

defaultTasks 'build'
